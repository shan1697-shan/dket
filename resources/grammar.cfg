S -> ARDEF _PERIOD EOS
S -> DEFPRED _PERIOD EOS

a -> 'a/DT'
all -> 'all/DT'
are -> 'are/VBP'
also -> 'also/RB'
at -> 'at/IN'
and -> 'and/CC'
any -> 'any/DT'
anything -> 'anything/NN'
but -> 'but/CC'
do -> 'do/VBP'
every -> 'every/DT'
everything -> 'everything/NN'
exactly -> 'exactly/RB'
is -> 'is/VBZ'
least -> 'least/JJS'
less -> 'less/JJR'
more -> 'more/JJR'
most -> 'most/JJS'
no -> 'no/DT'
nor -> 'nor/DT'
not -> 'not/RB'
of -> 'of/IN'
only -> 'only/RB'
or -> 'or/CC'
some -> 'some/DT'
something -> 'something/NN'
than -> 'than/IN'
that -> 'that/WDT'
the -> 'the/DT'
_COMMA -> ',/.'
_PERIOD -> './.'
EOS -> '<EOS>/<EOS>'

NN -> '@NN/NN'
JJ -> '@JJ/JJ'
NUM -> '@NUM/CD'
PRED -> '@VB/VB'

A^EVERY -> a | every
ARE^ALSO -> are | are also
AND^OR -> and | or
AND^OR^BUT -> and | or | but 
ALL^THE -> all | all the
COMMA^OR -> or | _COMMA or
COMMA^AND -> and | _COMMA and
# DO^NOT -> do not  # currently not used
IS^ALSO -> is | is also

ROLE -> SINGLE^PRED^ROLE | DOUBLE^PRED^ROLE

TH^THAT^ROLE -> ANYTH^THAT^ROLE | SOMETH^THAT^ROLE | EVERYTH^THAT^ROLE
ANYTH^THAT^ROLE -> anything that ROLE 
SOMETH^THAT^ROLE -> something that ROLE
EVERYTH^THAT^ROLE -> everything that ROLE

# 1. Aristotelian definition per genus et differentia.
#   1.1. differentia as a ROLE
A^EVERY^CLASS^NP -> A^EVERY CLASS^NP
ALL^THE^CLASS^NP -> ALL^THE CLASS^NP
CLASS^NP^THAT^ROLE -> CLASS^NP that ROLE
DIFF^SING -> a CLASS^NP^THAT^ROLE | SOMETH^THAT^ROLE
DIFF^PL -> CLASS^NP^THAT^ROLE | TH^THAT^ROLE
CLASS^CONJ^PL -> CLASS^NP AND^OR CLASS^NP
CLASS^CONJ^SING -> a CLASS^NP AND^OR a CLASS^NP
JJ^CLASS^NP -> JJ | CLASS^NP
DIFF^COPULA^PL -> CLASS^NP that are JJ^CLASS^NP
DIFF^COPULA^SING -> a CLASS^NP that is JJ^CLASS^NP

ARDEF -> A^EVERY^CLASS^NP IS^ALSO DIFF^SING
ARDEF -> A^EVERY^CLASS^NP IS^ALSO CLASS^CONJ^SING
ARDEF -> A^EVERY^CLASS^NP IS^ALSO DIFF^COPULA^SING
ARDEF -> ALL^THE^CLASS^NP ARE^ALSO CLASS^CONJ^PL
ARDEF -> ALL^THE^CLASS^NP ARE^ALSO CLASS^NP^THAT^ROLE
ARDEF -> CLASS^NP ARE^ALSO DIFF^PL
ARDEF -> CLASS^NP ARE^ALSO CLASS^CONJ^PL
ARDEF -> CLASS^NP ARE^ALSO DIFF^COPULA^PL

# 2. definition as predicate on the class.
DEFPRED -> CLASS^NP ROLE | A^EVERY^CLASS^NP ROLE | ALL^THE^CLASS^NP ROLE

CARD^EXACT -> NUM | exactly NUM
CARD^GT -> more than NUM
CARD^GTE -> at least NUM | not less than NUM
CARD^MIN -> CARD^GT | CARD^GTE 
CARD^LT -> less than NUM
CARD^LTE -> at most NUM | no more than NUM
CARD^MAX -> CARD^LT | CARD^LTE
SINGLE^CARD -> CARD^EXACT | CARD^MIN | CARD^MAX
DOUBLE^CARD -> CARD^MIN AND^OR^BUT CARD^MAX 
DOUBLE^CARD -> CARD^MAX AND^OR^BUT CARD^MIN
# CARD^ONE -> a -- currently not used.

REG^RANGE -> CLASS^NP
REG^RANGE -> OR^JJ^CHAIN | AND^JJ^CHAIN
REG^RANGE -> OR^NP^CHAIN
REG^RANGE -> OR^DET^CHAIN | AND^DET^CHAIN
FULL^RANGE -> REG^RANGE 
FULL^RANGE -> AND^NP^CHAIN

SINGLE^PRED^ROLE -> PRED FULL^RANGE
SINGLE^PRED^ROLE -> PRED only FULL^RANGE
SINGLE^PRED^ROLE -> PRED also FULL^RANGE
SINGLE^PRED^ROLE -> PRED SINGLE^CARD REG^RANGE
SINGLE^PRED^ROLE -> PRED DOUBLE^CARD REG^RANGE
SINGLE^PRED^ROLE -> SINGLE^ZERO^PRED REG^RANGE
SINGLE^ZERO^PRED -> do not PRED any | do not PRED | PRED no

DOUBLE^PRED^ROLE -> PRED^OR^PRED FULL^RANGE
DOUBLE^PRED^ROLE -> PRED^OR^PRED only FULL^RANGE
DOUBLE^PRED^ROLE -> PRED^OR^PRED also FULL^RANGE
DOUBLE^PRED^ROLE -> PRED^AND^PRED FULL^RANGE
DOUBLE^PRED^ROLE -> PRED^AND^PRED only FULL^RANGE
DOUBLE^PRED^ROLE -> PRED^AND^PRED also FULL^RANGE
DOUBLE^PRED^ROLE -> DOUBLE^ZERO^PRED REG^RANGE

PRED^OR^PRED -> PRED or PRED
PRED^AND^PRED -> PRED and PRED
DOUBLE^ZERO^PRED -> do not PRED nor PRED any | PRED or PRED no

CLASS^NP -> NP
NP -> NP^MIN | NP^OF^MIN | NP^LARGE
NP^MIN -> NN | JJ NN | NN NN | JJ NN NN
NP^OF^MIN -> NN of NN | JJ NN of NN | NN of JJ NN
NP^LARGE -> JJ JJ NN | NN NN of NN | NN of NN NN
OF^MIN -> of NN | of JJ NN

OR^JJ^CHAIN -> JJ or JJ NN | JJ _COMMA JJ COMMA^OR JJ NN
AND^JJ^CHAIN -> JJ and JJ NN | JJ _COMMA JJ COMMA^AND JJ NN
OR^NP^CHAIN -> NP^MIN or NP^MIN | NP^MIN _COMMA NP^MIN COMMA^OR NP^MIN
AND^NP^CHAIN -> NP^MIN and NP^MIN | NP^MIN _COMMA NP^MIN COMMA^AND NP^MIN
OR^DET^CHAIN -> NP^OF^MIN or OF^MIN
AND^DET^CHAIN -> NP^OF^MIN and OF^MIN